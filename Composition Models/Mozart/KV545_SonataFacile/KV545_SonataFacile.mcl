/*
 * Example composition model for Music Processing Suite
 * written in MC2L (Musical Context Composition Language)
 * 
 * Website: www.musicprocessing.net
 * 
 * License: www.musicprocessing.net/license/license.html
 * 
 * @author: David Hofmann <dev@davehofmann.de>
 */

title "Sonata Facile (KV 545), First Movement"
composer "W.A. Mozart"

composition
{
    fragment section0 internalSectionId "section0"
    {
        harmonicProgression I V7 I IV I V7 I
        {
            harmonicRhythm 1 2 2 2 2 2 2
            {
                parallel
                {

                    fragment part0melody
                    {
                        rhythm 2 4 4 4. 16 16 4 _4
                        {
                            pitches (startOctave 5) 0 2 4 -1 0 1 0
                            rhythmicInsertion mode overwrite offset 1 rhythm 4 8 16 16 4
                            {
                                pitches (startOctave 5) 5 4 7 4 3 2 3 2
                            }
                        }
                    }

                    fragment part0accompaniment
                    {
                        repeat 8
                        {
                            rhythm 8 8 8 8
                            {
                                arpeggioGenerator (numberOfNotes 3 noteIndexSequence 0 2 1 2)
                            }
                        }
                    }
                }
            }
        }
    }
    
    fragment section1 internalSectionId "section1"
    {
        harmonicProgression IV I/III ii 7 V I
        {
            harmonicRhythm 1 1 2. 4 1
            {
                parallel
                {
                    fragment part1melody
                    {
                        rhythm 8 16 16 16 16 16 16 16 16 16 16 16 16 16 16
                        {
                            pitches (startOctave 5) -2 -1 0 1 2 3 4 5 4 3 2 1 0 -1 -2
                            {
                                sequence 4 times step -1 mode inScale
                            }
                        }
                    }
                    
                    fragment part1accompainment
                    {
                        rhythm 4 _2 4
                        {
                            pitches (startOctave 3) 10 [3 7]
                            pitches (startOctave 3) [2 7] [2 7]
                            pitches (startOctave 3) [1 7] [1 6]
                            pitches (startOctave 3) [0 7] [0 2]
                        }
                    }
                }
            }
        }
    }
}